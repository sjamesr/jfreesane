plugins {
    id 'checkstyle'
    id 'java'
    id 'wrapper'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'maven-publish'
    id 'com.github.sherter.google-java-format' version '0.3.2'
    id 'net.ltgt.errorprone' version '1.3.0'

    // code coverage support
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.4'
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

def releaseVersion = '1.0'

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'com.google.truth:truth:0.24'

    compile 'com.google.errorprone:error_prone_annotations:2.4.0'

    // Cobertura requires slf4j at runtime
    testRuntime 'org.slf4j:slf4j-api:1.7.10'

    errorprone 'com.google.errorprone:error_prone_core:2.4.0'
    errorproneJavac 'com.google.errorprone:javac:9+181-r4173-1'
}

wrapper {
    gradleVersion = '6.7.1'
}

checkstyle {
    configFile = file('checkstyle_config.xml')
    ignoreFailures = false
    toolVersion = '6.19'
}

task checkstyleHtml {
    doLast {
        ant.xslt(in: checkstyleMain.reports.xml.destination,
                 style: file('checkstyle-noframes-sorted.xsl'),
                 out: new File(checkstyleMain.reports.xml.destination.parent, 'main.html'))
    }
}

checkstyleMain.finalizedBy checkstyleHtml

// Don't stop test from running because of checkstyle failures.
checkstyleTest.mustRunAfter test

javadoc {
    options.memberLevel = JavadocMemberLevel.PROTECTED
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    baseName 'javadoc'

    from javadoc.destinationDir
}

task sourceJar(type: Jar) {
    baseName 'sources'

    from sourceSets.main.allJava
}

def appendMavenCentralMetadata(Node node) {
    node.appendNode('name', 'JFreeSane')
    node.appendNode('description', 'A Java client for the SANE daemon')
    node.appendNode('url', 'http://github.com/sjamesr/jfreesane')

    def license = node.appendNode('licenses').appendNode('license')
    license.appendNode('name', 'The Apache Software License, Version 2.0')
    license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
    license.appendNode('distribution', 'repo')

    node.appendNode('scm').appendNode('url', 'https://github.com/sjamesr/jfreesane.git')

    def developerInfo = node.appendNode('developers').appendNode('developer')
    developerInfo.appendNode('id', 'dev')
    developerInfo.appendNode('name', 'The JFreeSane Contributors')
    developerInfo.appendNode('email', 'jfreesane-discuss@googlegroups.com')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier 'sources'
            }

            artifact javadocJar {
                classifier 'javadoc'
            }

            groupId 'com.googlecode.jfreesane'
            artifactId 'jfreesane'
            version releaseVersion

            pom.withXml {
                appendMavenCentralMetadata(asNode())
            }
        }
    }
}

bintray {
    user = project.properties.bintrayUsername
    key = project.properties.bintrayApiKey

    pkg {
        repo = 'maven'
        name = 'jfreesane'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/sjamesr/jfreesane.git'

        version {
            name = releaseVersion
            vcsTag = "jfreesane-${releaseVersion}"

            gpg {
                sign = true
            }
        }

    }

    publications = ['mavenJava']
}

// If Java formatter checks fail, tell the user how to fix them.
task printFormatHelperMessage {
    doLast {
        throw new GradleException('google-java-format is required.\n' +
            'Use ./gradlew googleJavaFormat to reformat the broken files')
    }
}
verifyGoogleJavaFormat.finalizedBy printFormatHelperMessage
printFormatHelperMessage.onlyIf { verifyGoogleJavaFormat.state.failure != null }
