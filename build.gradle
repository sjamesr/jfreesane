import com.vanniktech.maven.publish.JavaLibrary
import com.vanniktech.maven.publish.JavadocJar

plugins {
    id 'checkstyle'
    id 'java-library'
    id 'wrapper'
    id 'com.diffplug.spotless' version '7.2.1'
    id 'net.ltgt.errorprone' version '4.3.0'

    // code coverage support
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.4'

    // Publishing
    id 'com.vanniktech.maven.publish' version '0.34.0'
}

java {
  sourceCompatibility = targetCompatibility = JavaVersion.VERSION_11
}

def releaseVersion = '1.1'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.google.truth:truth:0.24'

    implementation 'com.google.errorprone:error_prone_annotations:2.4.0'

    // Cobertura requires slf4j at runtime
    testRuntimeOnly 'org.slf4j:slf4j-api:1.7.10'

    errorprone 'com.google.errorprone:error_prone_core:2.41.0'
    errorproneJavac 'com.google.errorprone:javac:9+181-r4173-1'
}

wrapper {
    gradleVersion = '9.0.0'
}

checkstyle {
    configFile = file('checkstyle_config.xml')
    ignoreFailures = false
    toolVersion = '11.0.1'

}

tasks.withType(Checkstyle) {
    reports {
        xml.required = false
        html.required = true
    }
}

// Don't stop test from running because of checkstyle failures.
checkstyleTest.mustRunAfter test

javadoc {
    options.memberLevel = JavadocMemberLevel.PROTECTED
}

mavenPublishing {
    configure(new JavaLibrary(new JavadocJar.Javadoc(), true))

    publishToMavenCentral()
    signAllPublications()

    coordinates("com.googlecode.jfreesane", "jfreesane", releaseVersion)

    pom {
        name = "JFreeSane"
        description = "A Java client for the SANE daemon"
        inceptionYear = "2020"
        url = "https://github.com/username/mylibrary/"
        licenses {
            license {
                name = "The Apache License, Version 2.0"
                url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }
        developers {
            developer {
                id = "dev"
                name = "The JFreeSane Contributors"
                email = "jfreesane-discuss@googlegroups.com"
            }
        }
        scm {
            url = "https://github.com/sjamesr/jfreesane/"
            connection = "scm:git:git://github.com/sjamesr/jfreesane.git"
            developerConnection = "scm:git:ssh://git@github.com/sjamesr/jfreesane.git"
        }
    }
}

spotless {
    java {
        googleJavaFormat('1.27.0').groupArtifact('com.google.googlejavaformat:google-java-format')
    }
}

